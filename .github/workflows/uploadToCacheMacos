#! /nix/var/nix/profiles/per-user/runner/profile/bin/ipso

# On Macos, `/usr/bin/env ipso` doesn't find `ipso` after I install it with Nix.
# I haven't figured out why.

getvar : String -> IO String
getvar key =
  comp
    bind mValue <- env.getvar key
    case mValue of
      None () ->
        comp
          println "missing environment variable: $key"
          exit.failure
      Some value ->
        io.pure value

binaryCacheBucket : String
binaryCacheBucket = "ipso-binary-cache"

binaryCacheEndpoint : String
binaryCacheEndpoint = "7065dc7f7d1813a29036535b4c4f4014.r2.cloudflarestorage.com"

main : IO ()
main =
  comp
    bind outPaths <- getvar "OUT_PATHS"
    println "Uploading paths $outPaths"
    let binaryCacheUrl = "s3://${binaryCacheBucket}?scheme=https&endpoint=$binaryCacheEndpoint&secret-key=/var/run/nix-signing-key"
    cmd.run `/nix/var/nix/profiles/default/bin/nix copy --to $binaryCacheUrl $..{string.partsc ' ' outPaths}`
